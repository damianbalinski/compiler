DECLARE
	ma(1:100), mb(1:100), mc(1:100), n, s, idx, idx_a, idx_b
BEGIN
	READ n; [ ### wczyatywanie rozmiaru macierzy]
	IF n > 10 THEN [ ### czy nie wykraczamy poza zakres - max. macierz 10x10 ]
		WRITE 0;
	ELSE
		[ ### wczytywanie macierzy ma i mb -> jednocześnie, wierszami ]
		FOR row FROM 1 TO n DO
			FOR col FROM 1 TO n DO 
				[ ### idx = (row-1)*n + col]
				idx := row - 1;
				idx := idx * n;
				idx := idx + col; 
				READ ma(idx);
				READ mb(idx);
			ENDFOR
		ENDFOR
		[ ### mc = ma*mb - mnożenie macierzy ]
		FOR i FROM 1 TO n DO
			FOR j FROM 1 TO n DO
				[ ### idx = (i-1)*n + j]
				idx := i - 1;
				idx := idx * n; 
				idx := idx + j;
				mc(idx) := 0; 
				FOR k FROM 1 TO n DO [ ### mc(i,j) = \sum_{k=1}^{n} ma(i,k)*mb(k,j) ]
					[ ### idx_a := (i-1)*n + k]
					idx_a := i - 1;
					idx_a := idx_a * n;
					idx_a := idx_a + k;			
					[ ### idx_b := (k-1)*n + j]
					idx_b := k - 1;
					idx_b := idx_b * n;
					idx_b := idx_b + j;
					s := ma(idx_a) * mb(idx_b);
					mc(idx) := mc(idx) + s;
				ENDFOR
			ENDFOR
		ENDFOR
		[ ### wypisywanie macierzy mc (wierszami, wiersze oddzielone 111111111) ]
		FOR row FROM 1 TO n DO
			WRITE 111111111;
			FOR col FROM 1 TO n DO 
				[ ### idx = (row-1)*n + col]
				idx := row - 1;
				idx := idx * n;
				idx := idx + col; 
				WRITE mc(idx);
			ENDFOR
		ENDFOR
	ENDIF
END

